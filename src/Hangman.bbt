REM >!Hangman
REM
REM Copyright 1996-2014, Stephen Fryatt (info@stevefryatt.org.uk)
REM
REM This file is part of Hangman:
REM
REM   http://www.stevefryatt.org.uk/software/
REM
REM Licensed under the EUPL, Version 1.1 only (the "Licence");
REM You may not use this work except in compliance with the
REM Licence.
REM
REM You may obtain a copy of the Licence at:
REM
REM   http://joinup.ec.europa.eu/software/page/eupl
REM
REM Unless required by applicable law or agreed to in
REM writing, software distributed under the Licence is
REM distributed on an "AS IS" basis, WITHOUT WARRANTIES
REM OR CONDITIONS OF ANY KIND, either express or implied.
REM
REM See the Licence for the specific language governing
REM permissions and limitations under the Licence.
:
REM Load Wimp Library, initialise set the error handler and poll until asked to exit.
:
LIBRARY "BASIC:Icon"
LIBRARY "BASIC:Menu"
LIBRARY "BASIC:Message"
LIBRARY "BASIC:Resources"
LIBRARY "BASIC:Template"
LIBRARY "BASIC:Url"
LIBRARY "BASIC:WimpError"
LIBRARY "BASIC:Window"
:
PROCwimperror_initialise("Hangman", "!hangman")
ON ERROR result% = FNwimperror_program : END
:
REM These values get replaced by tokenize.
:
build_version$ = "1.00"
build_date$ = "19 Jun 2002"
:
PROCinitialise
:
ON ERROR Quit% = FNwimperror_program
:
WHILE NOT Quit%
 PROCpoll
ENDWHILE
:
PROCclose_down
:
END
:
:
REM ********************************************************************************************
:

REM Initialise ourselves, internally and for the wimp.
:
DEF PROCinitialise
LOCAL task_name$, task_sprite$, resource_path$, time%

REM Set up quit flags and data blocks.

Quit% = FALSE

DIM a% 255, b% 4000, q% 255

resource_path$ = FNresources_find_territory_folder("<Hangman$Dir>.Resources")

REM Load the messages file.

PROCmessage_initialise(resource_path$ + "Messages")

task_name$ = FNmessage_lookup("TaskName")
task_sprite$ = FNmessage_lookup("TaskSpr")
PROCwimperror_initialise(task_name$, task_sprite$)

REM Register self with the wimp, asking for messages in q% to be sent to us.

!q% = &502
q%!4 = &400C1
q%!8 = &4E383
q%!12 = &4AF80
q%!16 = 0
SYS "Wimp_Initialise", 300, &4B534154, task_name$, q%

REM Load window templates and define windows.

ind_size% = 520
DIM ind_data% ind_size%

PROCtemplate_open(resource_path$ + "Templates")
PROCtemplate_load("Main", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO MainWindow%
PROCtemplate_load("Info", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO InfoWindow%
PROCtemplate_load("Options", b%, ind_data%, ind_size%, -1)
SYS "Wimp_CreateWindow",,b% TO OptionsWindow%
PROCtemplate_close

$FNicon_indirection(InfoWindow%, 4) = CHR$(169) + " Stephen Fryatt, 1996-" + MID$(build_date$, 8)
$FNicon_indirection(InfoWindow%, 6) = build_version$ + " (" + build_date$ + ")"

REM Load menu definitions and create menus.

PROCmenu_initialise
!b% = InfoWindow%
PROCmenu_load_templates(resource_path$ + "Menus", b%)
MainMenu% = !b%

REM Find the font we need to display the hangman text.

font_name$ = "Corpus.Bold"
font_size1% = 32
font_size2% = 12
SYS "Font_FindFont",,font_name$, font_size1%*16, font_size1%*16 TO display_font%
SYS "Font_FindFont",,font_name$, font_size2%*16, font_size2%*16 TO small_font%

REM Do the non-wimp initialiseation.

SYS "OS_ReadMonotonicTime" TO time%
IF time% >= 0 THEN time% = &FFFFFFFF - time%
time% = RND(time%)

SYS "Spell_ImpressionInfo" TO ,dictionary_size%

min_word_length% = 12
max_word_length% = 15
number_of_guesses% = 10

letter_colour% = 7
newletter_colour% = 11
fillin_colour% = 10
win_colour% = 8
dashmask_colour% = 15

PROCpick_new_word(min_word_length%, max_word_length%, number_of_guesses%)

REM Open the main window

PROCwindow_open(MainWindow%)
PROCicon_put_caret_at_end(MainWindow%, -1)
ENDPROC


REM ********************************************************************************************
:
:
DEF PROCclose_down
REM Shut down the application: deregister from the wimp and lose any outline fonts.
:
SYS "Font_LoseFont",display_font%
SYS "Font_LoseFont",small_font%
SYS "Wimp_CloseDown"
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCrebind_fonts
SYS "Font_LoseFont",display_font%
SYS "Font_LoseFont",small_font%
SYS "Font_FindFont",,font_name$,font_size1%*16,font_size1%*16 TO display_font%
SYS "Font_FindFont",,font_name$,font_size2%*16,font_size2%*16 TO small_font%
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCpoll
REM Poll the wimp and take any necessary action.
:
SYS "Wimp_Poll",0,b% TO reason%
:
CASE reason% OF
 WHEN 1     : PROCwindow_redraw
 WHEN 2     : SYS "Wimp_OpenWindow",,b%
 WHEN 3     : SYS "Wimp_CloseWindow",,b% : Quit%=TRUE
 WHEN 6     : PROChandle_mouse_clicks(b%!12,b%!16,b%!8,!b%,b%!4)
 WHEN 8     : PROChandle_key_presses(b%!24,!b%,b%!4)
 WHEN 9     : PROChandle_menu_selections(!b%,b%!4,b%!8,b%!12)
 WHEN 17,18 : PROChandle_wimp_message(b%)
 WHEN 19    : IF b%!16 = &4AF80 THEN PROCurl_bounce(b%)
ENDCASE
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROChandle_mouse_clicks(window%,icon%,buttons%,mouse_x%,mouse_y%)
CASE window% OF
 WHEN MainWindow%
  CASE buttons% OF
   WHEN 1,4
    CASE icon% OF
     WHEN 1
      IF playing% THEN
       PROCresign
      ELSE
       PROCpick_new_word(min_word_length%,max_word_length%,number_of_guesses%)
      ENDIF
     OTHERWISE : PROCicon_put_caret_at_end(MainWindow%,-1)
    ENDCASE
   WHEN 2 : PROCmenu_create(MainMenu%,mouse_x%,mouse_y%)
  ENDCASE
 WHEN OptionsWindow%
  CASE buttons% OF
   WHEN 1
    CASE icon% OF
     WHEN 2  : PROCicon_increment_value(OptionsWindow%,1,VAL($FNicon_indirection(OptionsWindow%,5)))
     WHEN 3  : PROCicon_decrement_value(OptionsWindow%,1,3)
     WHEN 6  : PROCicon_increment_value(OptionsWindow%,5,22)
     WHEN 7  : PROCicon_decrement_value(OptionsWindow%,5,VAL($FNicon_indirection(OptionsWindow%,1)))
     WHEN 10 : PROCicon_increment_value(OptionsWindow%,9,19)
     WHEN 11 : PROCicon_decrement_value(OptionsWindow%,9,1)
     WHEN 14 : PROCstore_options
    ENDCASE
   WHEN 4
    CASE icon% OF
     WHEN 2  : PROCicon_decrement_value(OptionsWindow%,1,3)
     WHEN 3  : PROCicon_increment_value(OptionsWindow%,1,VAL($FNicon_indirection(OptionsWindow%,5)))
     WHEN 6  : PROCicon_decrement_value(OptionsWindow%,5,VAL($FNicon_indirection(OptionsWindow%,1)))
     WHEN 7  : PROCicon_increment_value(OptionsWindow%,5,22)
     WHEN 10 : PROCicon_decrement_value(OptionsWindow%,9,1)
     WHEN 11 : PROCicon_increment_value(OptionsWindow%,9,19)
     WHEN 13 : PROCwindow_close(OptionsWindow%)
     WHEN 14 : PROCstore_options : PROCwindow_close(OptionsWindow%)
    ENDCASE
  ENDCASE
 WHEN InfoWindow%
  IF buttons% = 4 AND icon% = 8 THEN PROCurl_launch(FNmessage_lookup("SupportURL")) : PROCmenu_create(-1,0,0)
ENDCASE
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROChandle_menu_selections(s1%,s2%,s3%,s4%)
LOCAL adjust%
:
REM Set adjust% if the menu must be re-displayed (ie adjust selection).
:
SYS "Wimp_GetPointerInfo",,q%
adjust%=(q%!8=1)
:
CASE FNmenu_current_handle OF
 WHEN MainMenu%
  CASE s1% OF
   WHEN 1 : OSCLI("%Filer_Run <Hangman$Dir>.!Help")
   WHEN 2 : PROCshow_options(!q%,q%!4)
  ENDCASE
ENDCASE
:
IF adjust% THEN PROCmenu_create(FNmenu_current_handle,0,0)
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROChandle_key_presses(key%,window%,icon%)
CASE TRUE OF
 WHEN FNalphabetic(CHR$(key%)) : PROCmake_turn(CHR$(key%))
 WHEN key%=13        : IF playing% THEN
                        PROCresign
                       ELSE
                        PROCpick_new_word(min_word_length%,max_word_length%,number_of_guesses%)
                       ENDIF
 OTHERWISE : SYS "Wimp_ProcessKey",key%
ENDCASE
ENDPROC
:
:
REM ********************************************************************************************


REM Handle incoming user messages.
REM
REM \param message%		Poinnter to the message block.
:
DEF PROChandle_wimp_message(message%)

CASE message%!16 OF
	WHEN 0		: Quit%=TRUE
	WHEN &502	: PROCsend_interactive_help(message%)
	WHEN &400C1	: PROCrebind_fonts
	WHEN &4E383	: PROCurl_bounce(message%)
ENDCASE
ENDPROC


REM Process requests for interactive help text.
REM
REM \param message%		Pointer to the Message_HelpRequest block.
:
DEF PROCsend_interactive_help(message%)
LOCAL root$, suffix$, found%, message$

REM Compare the window handle to the windows we know about, and find the
REM relevant token name.

CASE message%!32 OF
	WHEN MainWindow%	: root$ = "Main"
	WHEN InfoWindow%	: root$ = "Info"
	WHEN OptionsWindow%	: root$ = "Options"
	OTHERWISE		: root$ = ""
ENDCASE

REM Look up the help text, either as a window message of by checking the
REM menus that belong to us.

IF root$ <> "" THEN
	suffix$ = FNicon_validation_command(message%!32, message%!36, "N")
	IF suffix$ <> "" THEN root$ += "." + suffix$
	found% = FNmessage_lookup_result("Help." + root$, message$)
ELSE
	SYS "Wimp_GetMenuState", 1, q%, message%!32, message%!36
	IF FNmenu_current_handle = MainMenu% THEN
		root$ = "MainMenu." + RIGHT$("00" + STR$(!q%), 2)
		found% = FNmessage_lookup_result("Help." + root$, message$)
	ENDIF
ENDIF

REM If no message was found, give up now.

IF NOT found% THEN ENDPROC

REM If a message was found, send it back to the requestor.

message%!12 = message%!8
message%!16 = &503
$(message% + 20) = message$ + CHR$(0)
!message% = 24+(LEN(message$) AND &FFFFFC)
SYS "Wimp_SendMessage", 17, message%, message%!4
ENDPROC


REM ********************************************************************************************
:
:
DEF PROCwindow_redraw
REM Redraw the main window: the hangman word is printed up 'by hand' to allow the
REM different colours to be shown.  The display offsets are calculated once for the
REM word when the new word is chosen.
REM This could be optimised a lot :-)
REM Can't notice on a RiscPC :->
:
LOCAL more%,loop%
SYS "Wimp_RedrawWindow",,b% TO more%
:
ox%=b%!4-b%!20
oy%=b%!16-b%!24
:
WHILE more%
 SYS "Font_SetFont",display_font%
 SYS "Wimp_SetFontColours",,1,dashmask_colour%
 SYS "Font_Paint",,word_mask$,&10,ox%+display_offset_x%,oy%+display_offset_y%
 SYS "Wimp_SetFontColours",,1,word_colour%
 SYS "Font_Paint",,word_fill$,&10,ox%+display_offset_x%,oy%+display_offset_y%
 SYS "Wimp_SetFontColours",,1,highlight_colour%
 SYS "Font_Paint",,word_last$,&10,ox%+display_offset_x%,oy%+display_offset_y%
 SYS "Font_SetFont",small_font%
 SYS "Wimp_SetFontColours",,1,7
 SYS "Font_Paint",,used_letters1$,&10,ox%+12,oy%-164
 SYS "Font_Paint",,used_letters2$,&10,ox%+12,oy%-192
 SYS "Wimp_SetColour",8
 IF turns_left%>0 THEN
  FOR loop%=1 TO turns_left%
   RECTANGLE FILL ox%+268+(loop%-1)*28,oy%-196,20,50
  NEXT loop%
 ENDIF
 SYS "Wimp_GetRectangle",,b% TO more%
ENDWHILE
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCpick_new_word(min%,max%,guesses%)
REM Pick a new word with a length within the limits and set it as the new word.
:
PROCset_new_word(FNget_word(min%,max%),guesses%)
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF FNget_word(min%,max%)
REM Pick a word from the Impression dictionary which has a length between the limits
REM specified.
:
LOCAL word%,word$
:
SYS "Hourglass_On"
:
REPEAT
 SYS "Spell_GetWord",RND(dictionary_size%) TO ,word%
 SYS "XOS_GenerateError", word% TO word$
UNTIL LEN(word$)>=min% AND LEN(word$)<=max%
:
SYS "Hourglass_Off"
:
=word$
:
:
REM ********************************************************************************************
:
:
DEF PROCset_new_word(word$,guesses%)
REM Set the given word as the new word to be guessed.  Reset all the variables.
:
LOCAL loop%,letter$
:
REM Blank the current word, and reset the used letters flags and print lists.  The
REM number of letters correctly guessed and the number of turns left are also reset.
:
current_word$=""
used_letters%=0
used_letters1$=STRING$(13," ")
used_letters2$=STRING$(13," ")
letters_hit%=0
turns_left%=guesses%
:
REM Go through the word supplied, and if each letter is alphabetic, convert it to
REM upper case and add it to the new word.
:
FOR loop%=1 TO LEN(word$)
 letter$=CHR$(ASC(MID$(word$,loop%,1)) AND &DF)
 IF FNalphabetic(letter$) THEN
  current_word$+=letter$
 ENDIF
NEXT loop%
:
REM word_mask$ contains the dashes for unguessed letters, word_fill$ and word_last$
REM contain a space for each letter.
:
word_mask$=STRING$(LEN(current_word$),"™") : REM The " " is in fact a "-"; zap doesn't show it!
word_fill$=STRING$(LEN(current_word$)," ")
word_last$=STRING$(LEN(current_word$)," ")
:
REM Calculate the display position of the new word.  The width of the text in the current
REM fint is caluculated and the offsets within the display icon are calculated.
:
SYS "Font_Converttopoints",,1064 TO ,box_x%
SYS "Font_SetFont",display_font%
SYS "Font_StringWidth",,current_word$,box_x%,font_size1%*16,32,LEN(current_word$) TO ,,box_x%
SYS "Font_ConverttoOS",,box_x%,font_size1%*1000 TO ,width%,height%
display_offset_x%=(1064-width%)/2+4
display_offset_y%=-128+((128-height%)/2)+14
:
REM Finally set the game in motion and force the window to update to show the new word.
:
playing%=TRUE
$FNicon_indirection(MainWindow%,1)=FNmessage_lookup("Resign")
word_colour%=letter_colour%
highlight_colour%=newletter_colour%
:
PROCicon_redraw(MainWindow%,0)
PROCicon_redraw(MainWindow%,1)
PROCicon_redraw(MainWindow%,2)
PROCicon_redraw(MainWindow%,3)
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCmake_turn(letter$)
REM This procedure handles a complete turn.  We only do it if there is a game in progress!
:
IF playing% THEN
 :
 REM Play the letter, then if we have run out of turns resign otherwise if the word is
 REM complete end.
 :
 PROCplay_letter(letter$)
 IF turns_left%=0 THEN playing%=FALSE : PROCresign
 IF letters_hit%=LEN(current_word$) THEN PROCfinish
ENDIF
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCfinish
REM This procedure completes a finished word.
:
REM Stop the game, update the button and the highlight colour.
:
playing%=FALSE
$FNicon_indirection(MainWindow%,1)=FNmessage_lookup("NewWord")
word_colour%=win_colour%
highlight_colour%=win_colour%
:
REM Redraw the button.
:
PROCicon_redraw(MainWindow%,1)
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCresign
REM This procedure abandons a word; either due to running out of guesses or because the
REM player asked to!
:
LOCAL loop%,letters_hit_now%
:
REM Stop the game, show the highlighs in orange, and update the button.
:
playing%=FALSE
$FNicon_indirection(MainWindow%,1)=FNmessage_lookup("NewWord")
word_colour%=letter_colour%
highlight_colour%=fillin_colour%
:
REM Go through the word_last$ copying any letters into the correct places in the
REM word_fill$ line.  This makes those letters in red now appear in black.
:
FOR loop%=1 TO LEN(current_word$)
 IF MID$(word_last$,loop%,1)<>" " THEN MID$(word_fill$,loop%,1)=MID$(word_last$,loop%,1)
NEXT loop%
:
REM Reset the red letters and rmove the mask dashes.
:
word_mask$=STRING$(LEN(current_word$)," ")
word_last$=STRING$(LEN(current_word$)," ")
:
REM Go through the word as guessed so far and put unguessed letters into word_last$ to
REM show up in highlight.
:
FOR loop%=1 TO LEN(current_word$)
 IF MID$(word_fill$,loop%,1)=" " THEN MID$(word_last$,loop%,1)=MID$(current_word$,loop%,1)
NEXT loop%
:
REM Redraw the screen.
:
PROCicon_redraw(MainWindow%,0)
PROCicon_redraw(MainWindow%,1)
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCplay_letter(letter$)
REM This procedure takes a letter and plays it against the current word.
:
LOCAL loop%,letters_hit_now%
:
REM Go through the word_last$ copying any letters into the correct places in the
REM word_fill$ line.  This makes those letters in red now appear in black.
:
FOR loop%=1 TO LEN(current_word$)
 IF MID$(word_last$,loop%,1)<>" " THEN MID$(word_fill$,loop%,1)=MID$(word_last$,loop%,1)
NEXT loop%
:
REM Reset the red letters.
:
word_last$=STRING$(LEN(current_word$)," ")
:
REM Put letter$ into upper case.
:
letter$=CHR$(ASC(letter$) AND &DF)
:
REM If letter$ has not yet been used, then play it.
:
IF NOT FNletter_used(letter$) THEN
 :
 REM Mark the letter as having been played, and zero a count to see how many letters we
 REM have hit in this turn.
 :
 PROCmark_off_letter(letter$)
 letters_hit_now%=0
 :
 REM For each letter in the word: if it is a hit, remove the dah from the mask, add the
 REM letter in red (word_last$) and count the hit.
 :
 FOR loop%=1 TO LEN(current_word$)
  IF MID$(current_word$,loop%,1)=letter$ THEN
   MID$(word_mask$,loop%,1)=" "
   MID$(word_last$,loop%,1)=letter$
   letters_hit_now%+=1
  ENDIF
 NEXT loop%
 :
 REM If no letters were hit (ie. a wrong guess), show the letter below, deduct a turn,
 REM and update the turn display.
 :
 IF letters_hit_now%=0 THEN
  PROCshow_letter_as_used(letter$)
  turns_left%-=1
  PROCicon_redraw(MainWindow%,3)
 ENDIF
 :
 REM Total the number of letters found.
 :
 letters_hit%+=letters_hit_now%
ENDIF
:
REM Redraw the word.
:
PROCicon_redraw(MainWindow%,0)
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF FNalphabetic(l$)
=INSTR("ABCDEFGHIJKLMNOPQRSTUVWXYZ",CHR$(ASC(l$) AND &DF))<>0
:
:
REM ********************************************************************************************
:
:
DEF FNletter_used(l$)
=(((used_letters% AND (1 << ((ASC(l$) AND &DF) - 65))) <> 0) AND FNalphabetic(l$))
:
:
REM ********************************************************************************************
:
:
DEF PROCmark_off_letter(l$)
IF FNalphabetic(l$) THEN used_letters%=used_letters% OR (1 << ((ASC(l$) AND &DF) - 65))
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCshow_letter_as_used(l$)
l$=CHR$(ASC(l$) AND &DF)
IF FNalphabetic(l$) THEN
 IF l$<="M" THEN
  MID$(used_letters1$,ASC(l$)-64,1)=l$
 ELSE
  MID$(used_letters2$,ASC(l$)-77,1)=l$
 ENDIF
 PROCicon_redraw(MainWindow%,2)
ENDIF
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCshow_options(x%,y%)
$FNicon_indirection(OptionsWindow%,1)=STR$(min_word_length%)
$FNicon_indirection(OptionsWindow%,5)=STR$(max_word_length%)
$FNicon_indirection(OptionsWindow%,9)=STR$(number_of_guesses%)
PROCwindow_open_at(OptionsWindow%,x%,y%)
ENDPROC
:
:
REM ********************************************************************************************
:
:
DEF PROCstore_options
min_word_length%=VAL($FNicon_indirection(OptionsWindow%,1))
max_word_length%=VAL($FNicon_indirection(OptionsWindow%,5))
number_of_guesses%=VAL($FNicon_indirection(OptionsWindow%,9))
ENDPROC
